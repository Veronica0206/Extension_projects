TITLE:      PBLSGM with an Unknown Fixed Knot
Data:       FILE = example_data.csv;
VARIABLE:   NAMES = id Y1-Y10 Z1-Z10 T1-T10; 
            USEVAR = Y1-Y10 Z1-Z10;
            CONSTRAINT = T1-T10;
! We first estimate the reparameterized the mean vector and var-cov matrix of construct-specific LVs
!(i.e., the measure at the knot, the mean and the half difference of two slopes). 
! mueta0s, mueta1s, and mueta2s are the means of such LVs;
! psi0s0s, psi1s1s, and psi2s2s are the variances of such LVs;
! psi0s1s, psi0s2s, and psi1s2s are the covariates of such LVs. 
! theta is the residual variance
MODEL:      eta0sY BY Y1-Y10@1;
            ! factor loadings for eta0s are set equal to 1
            eta1sY BY Y1* (YL11)
                      Y2* (YL12)
                      Y3* (YL13)
                      Y4* (YL14)
                      Y5* (YL15)
                      Y6* (YL16)
                      Y7* (YL17)
                      Y8* (YL18)
                      Y9* (YL19)
                      Y10* (YL110);
            eta2sY BY Y1* (YL21)
                      Y2* (YL22)
                      Y3* (YL23)
                      Y4* (YL24)
                      Y5* (YL25)
                      Y6* (YL26)
                      Y7* (YL27)
                      Y8* (YL28)
                      Y9* (YL29)
                      Y10* (YL210);
                      
            eta0sZ BY Z1-Z10@1;
            ! factor loadings for eta0s are set equal to 1
            eta1sZ BY Z1* (ZL11)
                      Z2* (ZL12)
                      Z3* (ZL13)
                      Z4* (ZL14)
                      Z5* (ZL15)
                      Z6* (ZL16)
                      Z7* (ZL17)
                      Z8* (ZL18)
                      Z9* (ZL19)
                      Z10* (ZL110);
            eta2sZ BY Z1* (ZL21)
                      Z2* (ZL22)
                      Z3* (ZL23)
                      Z4* (ZL24)
                      Z5* (ZL25)
                      Z6* (ZL26)
                      Z7* (ZL27)
                      Z8* (ZL28)
                      Z9* (ZL29)
                      Z10* (ZL210);
            ! Factor loadings for eta1s and eta2s are not fixed values,
            ! because they contain an estimate parameter, the fixed knot

            ! Population values of parameters:
            !! Original settings:
            !! mueta0Y: 98, mueta1Y: 5, mueta2Y: 2.6, mugY: 3.5
            !! mueta0Z: 102, mueta1Z: 5, mueta2Z: 2.6, mugZ: 5.5
            !! psi00: 25, psi01: 1.5, psi02: 1.5
            !!            psi11: 1.0, psi12: 0.3
            !!                        psi22: 1.0

            !! Reparameterized settings:
            !! psi0sY0sY: 47.750, psi0sY1sY: 3.775, psi0sY2sY: -1.225, psi0sY0sZ: 26.775, psi0sY1sZ: 2.550, psi0sY2sZ: 0.000
            !!                    psi1sY1sY: 0.650, psi1sY2sY: 0.000,  psi1sY0sZ: 3.150, psi1sY1sZ: 0.300, psi1sY2sZ: 0.000
            !!                                      psi2sY2sY: 0.350,  psi2sY0sZ: 0.000, psi2sY1sZ: 0.000, psi2sY2sZ: 0.000
            !!                                                         psi0sZ0sZ: 71.750, psi0sZ1sZ: 5.075, psi0sZ2sZ: -1.925
            !!                                                                            psi1sZ1sZ: 0.650, psi1sZ2sZ: 0.000  
            !!                                                                                              psi2sZ2sZ: 0.350
            
            !! theta: 1.0

            !! Construct Y
            eta0sY*45.0 (psi0sY0sY);
            eta1sY*0.5  (psi1sY1sY);
            eta2sY*0.5  (psi2sY2sY);

            eta0sY WITH eta1sY*4.0  (psi0sY1sY);
            eta0sY WITH eta2sY*-1.5 (psi0sY2sY);
            eta1sY WITH eta2sY*0    (psi1sY2sY);

            [eta0sY*120]  (mueta0sY);
            [eta1sY*4]    (mueta1sY);
            [eta2sY*-1]   (mueta2sY);

            Y1-Y10*1.1 (thetaY);
            [Y1-Y10@0];

            !! Construct Z
            eta0sZ*70.0 (psi0sZ0sZ);
            eta1sZ*0.5  (psi1sZ1sZ);
            eta2sZ*0.5  (psi2sZ2sZ);

            eta0sZ WITH eta1sZ*5.0 (psi0sZ1sZ);
            eta0sZ WITH eta2sZ*-2  (psi0sZ2sZ);
            eta1sZ WITH eta2sZ*0   (psi1sZ2sZ);

            [eta0sZ*120]  (mueta0sZ);
            [eta1sZ*4]    (mueta1sZ);
            [eta2sZ*-1]   (mueta2sZ);

            Z1-Z10*1.1 (thetaZ);
            [Z1-Z10@0];

            !! Association between Y and Z
            eta0sY WITH eta0sZ*28.0 (psi0sY0sZ);
            eta0sY WITH eta1sZ*2.5  (psi0sY1sZ);
            eta0sY WITH eta2sZ*0    (psi0sY2sZ);

            eta1sY WITH eta0sZ*3.2  (psi1sY0sZ);
            eta1sY WITH eta1sZ*0.3  (psi1sY1sZ);
            eta1sY WITH eta2sZ*0    (psi1sY2sZ);

            eta2sY WITH eta0sZ*0    (psi2sY0sZ);
            eta2sY WITH eta1sZ*0    (psi2sY1sZ);
            eta2sY WITH eta2sZ*0    (psi2sY2sZ);

            Y1 WITH Z1*0.3 (thetaYZ);
            Y2 WITH Z2*0.3 (thetaYZ);
            Y3 WITH Z3*0.3 (thetaYZ);
            Y4 WITH Z4*0.3 (thetaYZ);
            Y5 WITH Z5*0.3 (thetaYZ);
            Y6 WITH Z6*0.3 (thetaYZ);
            Y7 WITH Z7*0.3 (thetaYZ);
            Y8 WITH Z8*0.3 (thetaYZ);
            Y9 WITH Z9*0.3 (thetaYZ);
            Y10 WITH Z10*0.3 (thetaYZ);

MODEL CONSTRAINT: 
            ! In the model constraint, we have 3 parts:
            ! Part I: Specify extra parameters other than those related to transformed LVs
            new(mueta0Y mueta1Y mueta2Y mugY*3.5
                psi0Y0Y psi0Y1Y psi0Y2Y
                        psi1Y1Y psi1Y2Y
                                psi2Y2Y
                mueta0Z mueta1Z mueta2Z mugZ*5.5
                psi0Z0Z psi0Z1Z psi0Z2Z
                        psi1Z1Z psi1Z2Z
                                psi2Z2Z
                psi0Y0Z psi0Y1Z psi0Y2Z
                psi1Y0Z psi1Y1Z psi1Y2Z
                psi2Y0Z psi2Y1Z psi2Y2Z);
            
            ! Part II: Specify a series of equations to anti-transform the LVs
            mueta0Y = mueta0sY + mugY * (mueta2sY - mueta1sY);
            mueta1Y = mueta1sY - mueta2sY;
            mueta2Y = mueta1sY + mueta2sY;
            psi0Y0Y = (psi1sY1sY + psi2sY2sY - 2 * psi1sY2sY) * mugY^2 + 
                     2 * (psi0sY2sY - psi0sY1sY) * mugY + psi0sY0sY;
            psi0Y1Y = (2 * psi1sY2sY - psi1sY1sY - psi2sY2sY) * mugY +
                     (psi0sY1sY - psi0sY2sY);
            psi0Y2Y = (psi2sY2sY - psi1sY1sY) * mugY + 
                     (psi0sY1sY + psi0sY2sY);
            psi1Y1Y = psi1sY1sY + psi2sY2sY - 2 * psi1sY2sY;
            psi1Y2Y = psi1sY1sY - psi2sY2sY;
            psi2Y2Y = psi1sY1sY + psi2sY2sY + 2 * psi1sY2sY;

            mueta0Z = mueta0sZ + mugZ * (mueta2sZ - mueta1sZ);
            mueta1Z = mueta1sZ - mueta2sZ;
            mueta2Z = mueta1sZ + mueta2sZ;
            psi0Z0Z = (psi1sZ1sZ + psi2sZ2sZ - 2 * psi1sZ2sZ) * mugZ^2 + 
                     2 * (psi0sZ2sZ - psi0sZ1sZ) * mugZ + psi0sZ0sZ;
            psi0Z1Z = (2 * psi1sZ2sZ - psi1sZ1sZ - psi2sZ2sZ) * mugZ +
                     (psi0sZ1sZ - psi0sZ2sZ);
            psi0Z2Z = (psi2sZ2sZ - psi1sZ1sZ) * mugZ + 
                     (psi0sZ1sZ + psi0sZ2sZ);
            psi1Z1Z = psi1sZ1sZ + psi2sZ2sZ - 2 * psi1sZ2sZ;
            psi1Z2Z = psi1sZ1sZ - psi2sZ2sZ;
            psi2Z2Z = psi1sZ1sZ + psi2sZ2sZ + 2 * psi1sZ2sZ;

            psi0Y0Z = (psi1sY1sZ + psi2sY2sZ - psi1sY2sZ - psi2sY1sZ) * 
                      mugY * mugZ + (psi2sY0sZ - psi1sY0sZ) * mugY + 
                      (psi0sY2sZ - psi0sY1sZ) * mugZ + psi0sY0sZ;
            psi0Y1Z = (psi1sY2sZ + psi2sY1sZ - psi1sY1sZ - psi2sY2sZ) * mugY +
                     (psi0sY1sZ - psi0sY2sZ);
            psi0Y2Z = (psi2sY1sZ - psi1sY2sZ - psi1sY1sZ + psi2sY2sZ) * mugY +
                     (psi0sY1sZ + psi0sY2sZ);
            psi1Y0Z = (psi1sY2sZ + psi2sY1sZ - psi1sY1sZ - psi2sY2sZ) * mugZ +
                     (psi1sY0sZ - psi2sY0sZ);
            psi1Y1Z = psi1sY1sZ + psi2sY2sZ - psi1sY2sZ - psi2sY1sZ;
            psi1Y2Z = psi1sY1sZ - psi2sY2sZ + psi1sY2sZ - psi2sY1sZ;
            psi2Y0Z = (psi1sY2sZ - psi2sY1sZ - psi1sY1sZ + psi2sY2sZ) * mugZ +
                     (psi1sY0sZ + psi2sY0sZ);
            psi2Y1Z = psi1sY1sZ - psi2sY2sZ - psi1sY2sZ + psi2sY1sZ;
            psi2Y2Z = psi1sY1sZ + psi2sY2sZ + psi1sY2sZ + psi2sY1sZ;

            ! Part III: Define factor loadings for the 2nd and 3rd LVs with
            ! definition variables

            YL11 = T1 - mugY;
            YL12 = T2 - mugY;
            YL13 = T3 - mugY;
            YL14 = T4 - mugY;
            YL15 = T5 - mugY;
            YL16 = T6 - mugY;
            YL17 = T7 - mugY;
            YL18 = T8 - mugY;
            YL19 = T9 - mugY;
            YL110 = T10 - mugY;

            YL21 = sqrt((T1 - mugY)^2);
            YL22 = sqrt((T2 - mugY)^2);
            YL23 = sqrt((T3 - mugY)^2);
            YL24 = sqrt((T4 - mugY)^2);
            YL25 = sqrt((T5 - mugY)^2);
            YL26 = sqrt((T6 - mugY)^2);
            YL27 = sqrt((T7 - mugY)^2);
            YL28 = sqrt((T8 - mugY)^2);
            YL29 = sqrt((T9 - mugY)^2);
            YL210 = sqrt((T10 - mugY)^2);

            ZL11 = T1 - mugZ;
            ZL12 = T2 - mugZ;
            ZL13 = T3 - mugZ;
            ZL14 = T4 - mugZ;
            ZL15 = T5 - mugZ;
            ZL16 = T6 - mugZ;
            ZL17 = T7 - mugZ;
            ZL18 = T8 - mugZ;
            ZL19 = T9 - mugZ;
            ZL110 = T10 - mugZ;

            ZL21 = sqrt((T1 - mugZ)^2);
            ZL22 = sqrt((T2 - mugZ)^2);
            ZL23 = sqrt((T3 - mugZ)^2);
            ZL24 = sqrt((T4 - mugZ)^2);
            ZL25 = sqrt((T5 - mugZ)^2);
            ZL26 = sqrt((T6 - mugZ)^2);
            ZL27 = sqrt((T7 - mugZ)^2);
            ZL28 = sqrt((T8 - mugZ)^2);
            ZL29 = sqrt((T9 - mugZ)^2);
            ZL210 = sqrt((T10 - mugZ)^2);
 OUTPUT:    SAMPSTAT;











            